ext {
  opennlpVersion = "1.8.2"
  guavaVersion = "23.0"
  lombokVersion = "1.16.18"
  dl4jVersion = "0.9.1"
  dropWizardVersion = "1.1.4"
}

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
  }
}

allprojects {
  apply plugin: "java"
  apply plugin: "idea"

  repositories {
    mavenCentral()
  }

  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"
}

subprojects {
  group = "io.aaronbatilo.summarybox"
}

project(":sbtk") {
  dependencies {
    compile "com.google.guava:guava:$guavaVersion"
    compile "io.dropwizard:dropwizard-core:$dropWizardVersion"
    compile "org.apache.opennlp:opennlp-tools:$opennlpVersion"
    compile "org.deeplearning4j:deeplearning4j-core:$dl4jVersion"
    compile "org.deeplearning4j:deeplearning4j-nlp:$dl4jVersion"
    compile "org.nd4j:nd4j-native-platform:$dl4jVersion"

    // https://mvnrepository.com/artifact/gov.nist.math/jama
    compile group: 'gov.nist.math', name: 'jama', version: '1.0.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
  }
}

project(":summarybox-service") {
  apply plugin: 'com.github.johnrengelman.shadow'
  apply plugin: 'application'

  mainClassName = "SummaryBoxApplication"

  shadowJar {
    archiveName = "summarybox.jar"
    destinationDir projectDir
  }

  dependencies {
    compile project(":sbtk")
    compile "io.dropwizard:dropwizard-core:$dropWizardVersion"
    compile "org.projectlombok:lombok:$lombokVersion"
    testCompile group: 'junit', name: 'junit', version: '4.12'
  }
}
